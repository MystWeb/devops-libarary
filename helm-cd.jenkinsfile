// 加载共享库
@Library("mylib@main") _

// 导入库

import org.devops.Checkout
import org.devops.GitLab
import org.devops.Kubernetes
import org.devops.ProjectCustom

// New实例化
def checkout = new Checkout()
def gitlab = new GitLab()
def kubernetes = new Kubernetes()
def projectCustom = new ProjectCustom()
pipeline {
    agent {
        label "k8s"
    }

    options {
        skipDefaultCheckout true
        buildDiscarder logRotator(daysToKeepStr: '30', numToKeepStr: '30')
    }

    parameters {
        string defaultValue: 'RELEASE-1.2.0', description: '注意：选择应用发布分支', name: 'branchName'
        string defaultValue: 'http://192.168.100.150/devops/devops-k8s-deployment.git', description: '注意：Helm Chart仓库地址', name: 'helmSrcUrl'
        string defaultValue: 'main', description: '注意：Helm Chart分支名称', name: 'helmBranchName'
        choice choices: ['dev01', 'dev02', 'test01', 'test02'], description: '环境列表', name: 'envList'
        choice choices: ['devops.demo.ui', 'devops.demo.service'], description: '服务访问的域名（注意：域名末尾会追加环境选择：domainName.dev01）', name: 'domainName'
        string defaultValue: '80', description: '注意：服务监听的端口号', name: 'port'
    }

    stages {
        stage("Global") {
            steps {
                script {
                    // 任务名称截取构建类型（任务名称示例：devops-maven-service）
//                    env.buildType = "${JOB_NAME}".split("-")[1]
                    // JOB任务前缀（业务名称/组名称）
                    env.buName = "${JOB_NAME}".split('-')[0]
                    // 产品名称
                    env.productName = "${JOB_NAME}".split('-')[1]
                    // 服务/项目名称
                    env.serviceName = "${JOB_NAME}".split('_')[0]
                    // GitLab访问凭据Id：GitLab-admin-账号密码（192.168.100.150）
                    env.credentialsId = "6271c438-8197-4392-a45e-94efd9f7afa3"
                    // GitLab用户（Admin）Token访问凭据Id：GitLab-admin-token（192.168.100.150）
                    env.gitlabUserTokenCredentialsId = "07163983-8c80-4c4b-bd63-dc5261f23117"
                    // Git项目Id
                    env.projectId = gitlab.GetProjectId("${env.gitlabUserTokenCredentialsId}", "${env.buName}", "${env.serviceName}")
                    // Git提交ID
                    env.commitId = gitlab.GetShortCommitIdByApi("${env.gitlabUserTokenCredentialsId}", "${env.projectId}", "${env.branchName}")
                    // 服务版本号（推荐定义："${branchName}-${commitId}"）
                    env.version = "${env.branchName}-${env.commitId}"
                    // 服务访问的域名
                    env.domainName = "${env.domainName}.${env.envList}"
                    // 服务部署的命名空间
                    env.namespace = "${env.buName}-${env.productName}-${env.envList}"

                    // 修改Jenkins构建描述
                    currentBuild.description = """ branchName：${env.branchName} \n commitId：${env.commitId} \n namespace：${env.namespace} \n domainName：${env.domainName} """
                    // 修改Jenkins构建名称
                    currentBuild.displayName = "${env.version}"
                }
            }
        }

        stage("Checkout") {
            steps {
                script {
                    sh "[ -d ${env.namespace} ] || mkdir ${env.namespace}"
                    ws("${WORKSPACE}/${env.namespace}") {
                        checkout.GetCode("${env.helmSrcUrl}", "${env.helmBranchName}", "${env.credentialsId}")
                    }
                }
            }
        }

        stage("HelmReleaseFile") {
            steps {
                script {
                    // Git文件模板名称
                    fileName = "values.yaml"
                    // Git模板文件路径：项目服务名称_Helm/values.yaml
                    filePath = "${env.namespace}/${env.serviceName}/${fileName}"
                    // 项目定制参数
                    Map projectParamsMap = projectCustom.getProjectParamsMap("${env.envList}")
                    // Kubernetes发布模板文件内容替换
                    if (projectParamsMap == null) {
                        kubernetes.HelmReleaseTemplateFileReplace("${filePath}", "${env.domainName}", "${env.port}")
                    } else {
                        kubernetes.HelmReleaseTemplateFileReplace("${filePath}", "${env.domainName}", "${env.port}", projectParamsMap)
                    }
                }
            }
        }

        stage("HelmDeploy") {
            steps {
                script {
                    kubernetes.HelmDeploy("${env.namespace}", "${env.namespace}/${env.serviceName}", "${env.serviceName}")
                }
            }
        }

        stage("HealthCheck") {
            steps {
                script {
                    // 注意：自定义域名需配置Hosts文件！
                    try {
                        sleep(5)
                        result = sh returnStdout: true, script: """ curl "http://${env.domainName}/health" """ - "\n"
                        if ("ok" == result) {
                            println("Successful！")
                        }
                    } catch (Exception e) {
                        println(e)
                    }
                }
            }
        }

        stage("RollOut") {
            input {
                message "是否进行回滚"
                ok "提交"
                submitter "admin,devops-dev,devops-test"
                parameters {
                    choice(choices: ['no', 'yes'], name: 'opts')
                }
            }

            steps {
                script {
                    switch ("${opts}") {
                        case "yes":
                            // 注意：声明式：input {} 中 choices 无法获取变量值，所以此处改为脚本式
                            def result = input message: "选择回滚版本?",
                                    parameters: [choice(choices: "${env.REVISION}", name: 'revision')]
                            println("选择回滚版本：${result}")
                            sh "helm rollback ${env.serviceName} ${result} -n ${env.buName}"
                            break
                        case "no":
                            break
                    }
                }
            }
        }

    }

    post {
        always {
            // clean workspace build
            cleanWs()
        }
    }

}